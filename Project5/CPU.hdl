// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/CPU.hdl

/**
 * The Hack CPU (Central Processing unit), consisting of an ALU,
 * two registers named A and D, and a program counter named PC.
 * The CPU is designed to fetch and execute instructions written in 
 * the Hack machine language. In particular, functions as follows:
 * Executes the inputted instruction according to the Hack machine 
 * language specification. The D and A in the language specification
 * refer to CPU-resident registers, while M refers to the external
 * memory location addressed by A, i.e. to Memory[A]. The inM input 
 * holds the value of this location. If the current instruction needs 
 * to write a value to M, the value is placed in outM, the address 
 * of the target location is placed in the addressM output, and the 
 * writeM control bit is asserted. (When writeM==0, any value may 
 * appear in outM). The outM and writeM outputs are combinational: 
 * they are affected instantaneously by the execution of the current 
 * instruction. The addressM and pc outputs are clocked: although they 
 * are affected by the execution of the current instruction, they commit 
 * to their new values only in the next time step. If reset==1 then the 
 * CPU jumps to address 0 (i.e. pc is set to 0 in next time step) rather 
 * than to the address resulting from executing the current instruction. 
 */

CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset==1) or continue executing
                         // the current program (reset==0).

    OUT outM[16],        // M value output
        writeM,          // Write to M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction

    PARTS:
    // Put your code here:
    Not(in = instruction[15],out = empty);
    Mux16(a = instruction,b = outalu,sel = instruction[15],out = inA);    
    Or(a = empty,b = instruction[5],out = loadA);
    ARegister(in = inA,load = loadA,out = Aout,out[0..14] = addressM);
    Mux16(a = Aout,b = inM,sel=instruction[12],out = AMout);
    Or(a = instruction[8],b = empty,out = ny);
    Or(a = instruction[9],b = empty,out = zy);
    And(a = instruction[11],b = instruction[15],out = zx);
    And(a = instruction[10],b = instruction[15],out = nx);
    And(a = instruction[7],b = instruction[15],out = f);
    And(a = instruction[6],b = instruction[15],out = no);
    ALU(x = Dout,y = AMout,zx = zx,nx = nx,zy = zy,ny = ny,f = f,no = no,out = outM,out = outalu,zr = zero,ng = neg);
    And(a = instruction[15],b = instruction[4],out = loadD);
    And(a = instruction[15],b = instruction[3],out = writeM);
    DRegister(in = outalu,load = loadD,out = Dout);
    Or(a = zero,b = neg,out = notpos);
    Not(in = notpos,out = pos);
    And(a = instruction[1],b = zero,out = j2);
    And(a = instruction[2],b = neg,out = j1);
    And(a = instruction[0],b = pos,out = j3);
    Or(a = j12,b = j3,out = jend);
    Or(a = j1,b=j2,out = j12);
    And(a = jend,b = instruction[15],out = jump);
    PC(in = Aout,load = jump,reset = reset,inc = true,out[0..14] = pc);
}